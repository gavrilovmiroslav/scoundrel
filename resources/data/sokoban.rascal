
unique terrain: field of symbol;
event MoveIntent { e: entity, p: num, q: num };

[Spawn Player On Start]
on Start
{
    spawn player with
        IsPlayer
      & IsAt(20, 20)
      & HasGlyph('@')
      & HasColor(0, 0, 255);

    for i in 0 to 10 {
        let x = rand(2, 50);
        let y = rand(2, 30);
        let w = rand(5, 15);
        let h = rand(5, 10);

        terrain[rectangle(x, y; x + w, y + h)] = '#';
    }
}

// ===== KEYBOARD EVENTS =====================================

[Move By Keyboard|first]
on KeyPress(key, "Pressed")
with e.IsPlayer & e.IsAt(x, y)
{
    let p = x, q = y;
    match key {
        case "Left": p = x - 1;
        case "Right": p = x + 1;
        case "Up": q = y - 1;
        case "Down": q = y + 1;
    } then {
        trigger MoveIntent(e, p, q);
        consume event;
    }
}

[Do Move Intent|last]
on MoveIntent(e, p, q)
with e.IsPlayer & e.IsAt(x, y)
{
    x = p;
    y = q;
    consume event;
}

[Draw Terrain]
with unique(terrain)
{
    print terrain;
}