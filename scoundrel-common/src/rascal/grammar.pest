WHITESPACE =  _{ " " | "\t" | "\r" | "\n" }
COMMENT    = { "//" ~ (!NEWLINE ~ ANY)* ~ NEWLINE }

decl = _{ (COMMENT | func_decl | system_decl | tag_decl | state_decl | event_decl | unique_decl) }
program = _{ (decl ~ decl*) }

// text

inner = @{ char* }
char = {
    !("\"" | "\\") ~ ANY
    | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
}

// components

name = @{ (ASCII_ALPHANUMERIC | "_" | "@")+ }

scalar_type = { "bool" | "entity" | "symbol" | "num" | "text" | "geometry" }
ref_type = { "&" ~ scalar_type }

value = _{ proc_call | random | set_query | bool_value | number | text_value | identifier | symbol }
random = { "rand" ~ "(" ~ expression ~ "," ~ expression ~ ")" }
indirect_set_query = _{ set_query | identifier }
set_query = _{ make_set_query | empty_query | rect_query
            | circle_query | line_query | union_query | intersect_query | diff_query
            | shrink_query | expand_query }

proc_call = { name ~ "(" ~ proc_param_list ~ ")" }
proc_param_list = { (proc_param ~ ((";" | ",") ~ proc_param)*) }
proc_param = { name ~ ":" ~ (proc_reference | expression) }
proc_reference = { "&" ~ name }

make_set_query = { "make_set()" }
empty_query = { "empty" ~ "(" ~ indirect_set_query ~ ")" }
rect_query = { "rectangle" ~ "(" ~ expression ~ "," ~ expression ~ ";" ~ expression ~ "," ~ expression ~ ")" }
circle_query = { "circle" ~ "(" ~ expression ~ "," ~ expression ~ ";" ~ expression ~ ")" }
line_query = { "line" ~ "(" ~ expression ~ "," ~ expression ~ ";" ~ expression ~ "," ~ expression ~  ")" }
union_query = { "union" ~ "(" ~ indirect_set_query ~ "," ~ indirect_set_query ~ ")" }
intersect_query = { "intersect" ~ "(" ~ indirect_set_query ~ "," ~ indirect_set_query ~ ")" }
diff_query = { "diff" ~ "(" ~ indirect_set_query ~ "," ~ indirect_set_query ~ ")" }
shrink_query = { "shrink" ~ "(" ~ indirect_set_query ~ "," ~ expression ~ ")" }
expand_query = { "expand" ~ "(" ~ indirect_set_query ~ "," ~ expression ~ ")" }

bool_value = { "true" | "false" }
number = { "-"? ~ ASCII_DIGIT+ }
text_value = { inner_text }
inner_text = { "\"" ~ inner ~ "\"" }
array_accessor = { name ~ "[" ~ expression ~ "]" }
field_accessor = { name ~ "[" ~ expression ~ "," ~ expression ~ "]" }
identifier = { name }
general_accessor = _{ array_accessor | field_accessor | identifier }
symbol = _{ "'" ~ any_char ~ "'" }
any_char = { ASCII }

// components
tag_decl = { "tag" ~ name  ~ ";" }
state_decl = { "state" ~ name ~ "{" ~ type_member_list ~ "}" ~ ";" }
event_decl = { "event" ~ name ~ "{" ~ type_member_list? ~ "}" ~ ";" }
unique_decl = { "unique" ~ name ~ ":" ~ datatype ~ ";" }
func_decl = { "proc" ~ name ~ "(" ~ type_member_list ~ ")" ~ block }
datatype = _{ ref_type | field | scalar_type }
field = { "field" ~ "of" ~ scalar_type }
type_member_list = { (type_member ~ ((";" | ",") ~ type_member)*) }
type_member = { name ~ ":" ~ datatype }

component_call_list = { component_call ~ ("&" ~ component_call)* }
component_call = _{ unique_call | negated_call_site | toggled_call_site | call_site }
component_arg_list = _{ "(" ~ arg_member_list ~ ")" }
negated_call_site = { "not:"  ~ call_site }
toggled_call_site = { "toggle:"  ~ call_site }
call_site = _{ (complex_call_site | simple_call_site) }
simple_call_site = { name ~ component_arg_list? }
complex_call_site = { owner_name? ~ "." ~ comp_name ~ component_arg_list? }
unique_call = { "unique" ~ "(" ~ name ~ ")" }

arg_member_list = { (arg_member ~ ("," ~ arg_member)*) }
arg_member = { expression }

owner_name = @{ name }
comp_name = @{ name }
// systems

system_title = { "[" ~ system_title_name ~ ("|" ~ system_priority)? ~ "]" }
system_title_name = @{ (ASCII_ALPHANUMERIC | "_" | "@" | " " | ":" | "-" | "," | ".")+ }
system_priority = { priority }
priority = { number | "last" | "first" | "game" | "ui" }
system_decl = { system_title ~ on_event_clause? ~ with_query_clause? ~ block }

block = { "{" ~ statements ~ "}" }
statements = _{ statement* }
statement = _{
    self_mod_assignment | for_loop_statement |
    mod_assignment | assignment |
    if_statement | match_statement |
    spawn_statement | destroy_statement |
    update_statement | trigger_statement |
    consume_statement | print_statement |
    paint_statement | let_statement |
    debug_statement | quit_statement |
    proc_call_statement
}

debug_statement = _{
    debug_print_statement
}

assignment = { general_accessor ~ "=" ~ expression ~ ";" }

self_mod_assignment = { general_accessor ~ self_mod_operator ~ ";" }
self_mod_operator = { "++" | "--" }

mod_assignment = { general_accessor ~ mod_operator ~ expression ~ ";" }
mod_operator = { "+=" | "-=" }

if_statement = { "if" ~ expression ~ block ~ else_block? }
else_block = { "else" ~ (if_statement | block) }

let_statement = { "let" ~ new_value_binding ~ ("," ~ new_value_binding)* ~ ";" }
new_value_binding = { identifier ~ "=" ~ expression }

for_loop_statement = { "for" ~ identifier ~ "in" ~ expression ~ "to" ~ expression ~ block }
match_statement = { "match" ~ expression ~ "{" ~ match_case+ ~ "}" ~ then_block? ~ else_block? }
match_case = { "case" ~ expression ~ ":" ~ (block | statement) }
then_block = { "then" ~ block }

spawn_statement = { "spawn" ~ identifier ~ "with" ~ component_call_list ~ ";" }
consume_statement = { "consume" ~ "event" ~ ";" }
destroy_statement = { "destroy" ~ identifier ~ ";" }
update_statement = { "update" ~ identifier ~ "with" ~ component_call_list ~ ";" }
trigger_statement = { "trigger" ~ call_site ~ ";" }
print_statement = { "print" ~ position? ~ foreground? ~ background? ~ expression ~ ";" }
paint_statement = { "paint" ~ position? ~ foreground? ~ background? ~ ";" }
proc_call_statement = { proc_call ~ ";" }
quit_statement = { "quit" ~ ";" }

position = { "at:[" ~ expression ~ "," ~ expression ~ "]" }
foreground = { "fg:[" ~ expression ~ "," ~ expression ~ "," ~ expression ~ "]" }
background = { "bg:[" ~ expression ~ "," ~ expression ~ "," ~ expression ~ "]" }

debug_print_statement = { "debug" ~ "print" ~ expression ~ ";" }

on_event_clause = { "on" ~ component_call }
with_query_clause = { "with" ~ component_call_list }

bool_operation = _{ and_op | or_op }
    and_op = { "&&" }
    or_op = { "||" }

binary_relation = _{ eq_rel | ne_rel | lt_rel | gt_rel | le_rel | ge_rel }
    eq_rel = { "==" }
    ne_rel = { "!=" }
    lt_rel = { "<" }
    gt_rel = { ">" }
    le_rel = { "<=" }
    ge_rel = { ">=" }

additive_op = _{ plus_op | minus_op }
    plus_op = { "+" }
    minus_op = { "-" }

multiplicative_op = _{ mul_op | div_op | mod_op }
    mul_op = { "*" }
    div_op = { "/" }
    mod_op = { "%" }

unary_op = _{ not_op | neg_op }
    not_op = { "!" }
    neg_op = { "-" }

expression = { bin ~ (bool_operation ~ bin)* }
bin = _{ additive ~ (binary_relation ~ additive)* }
additive = _{ summand ~ (additive_op ~ summand)* }
summand = _{ factor ~ (multiplicative_op ~ factor)* }
factor = _{ primary }
negated_primary = { unary_op ~ primary }
primary = _{ negated_primary | "(" ~ expression ~ ")" |
             complex_call_site | array_accessor |
             field_accessor | value }
